# Copyright 2022 The Dawn Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

################################################################################
# A copy of src/dawn/tests/BUILD.gn
# These targets are referenced by external code. We cannot simply alias the
# targets in src/dawn/tests/BUILD.gn due to an issue with resolving runtime
# dependencies via 'group' aliases. This whole file should be removed once
# the external targets are updated to use the new BUILD.gn file.
# TODO(crbug.com/dawn/1275) - remove this file.
################################################################################

import("../../scripts/dawn_overrides_with_defaults.gni")

import("//testing/test.gni")
import("${dawn_root}/generator/dawn_generator.gni")
import("${dawn_root}/scripts/dawn_features.gni")

group("tests") {
  testonly = true
  deps = [
    ":dawn_end2end_tests",
    ":dawn_perf_tests",
    ":dawn_unittests",
  ]
}

###############################################################################
# Gtest Gmock - Handle building inside and outside of Chromium.
###############################################################################

# When building outside of Chromium we need to define our own targets for GTest
# and GMock. However when compiling inside of Chromium we need to reuse the
# existing targets, both because Chromium has a special harness for swarming
# and because otherwise the "gn check" fails.

if (!build_with_chromium) {
  # When we aren't in Chromium we define out own targets based on the location
  # of the googletest repo.
  googletest_dir = dawn_googletest_dir

  config("gtest_config") {
    include_dirs = [
      "${googletest_dir}/googletest",
      "${googletest_dir}/googletest/include",
    ]
  }

  static_library("gtest") {
    testonly = true
    sources = [ "${googletest_dir}/googletest/src/gtest-all.cc" ]
    public_configs = [ ":gtest_config" ]
  }

  config("gmock_config") {
    include_dirs = [
      "${googletest_dir}/googlemock",
      "${googletest_dir}/googlemock/include",
      "${googletest_dir}/googletest/include",
    ]
  }

  static_library("gmock") {
    testonly = true
    sources = [ "${googletest_dir}/googlemock/src/gmock-all.cc" ]
    public_configs = [ ":gmock_config" ]
  }

  group("gmock_and_gtest") {
    testonly = true
    public_deps = [
      ":gmock",
      ":gtest",
    ]
  }
} else {
  # When we are in Chromium we reuse its targets, and also add some deps that
  # are needed to launch the test in swarming mode.
  group("gmock_and_gtest") {
    testonly = true
    public_deps = [
      "//base",
      "//base/test:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

###############################################################################
# Wrapping of Chromium targets
###############################################################################

# These targets are separated because they are Chromium sources files that
# can't use the dawn_internal config, otherwise Dawn's warning flags get
# applied while compiling a bunch of Chromium's //base (via header inclusion)
if (build_with_chromium) {
  source_set("unittests_main") {
    testonly = true
    deps = [ ":gmock_and_gtest" ]
    sources = [ "//gpu/dawn_unittests_main.cc" ]
  }
  source_set("end2end_tests_main") {
    testonly = true
    deps = [ ":gmock_and_gtest" ]
    sources = [ "//gpu/dawn_end2end_tests_main.cc" ]
  }
  source_set("perf_tests_main") {
    testonly = true
    deps = [ ":gmock_and_gtest" ]
    sources = [ "//gpu/dawn_perf_tests_main.cc" ]
  }
}

###############################################################################
# Dawn test template
###############################################################################
template("dawn_test") {
  test(target_name) {
    # Copy all variables except "configs", which has a default value
    forward_variables_from(invoker, "*", [ "configs" ])
    if (defined(invoker.configs)) {
      configs += invoker.configs
    }

    configs += [ "${dawn_root}/src/dawn/common:internal_config" ]

    output_name = "${target_name}_old"
  }
}

###############################################################################
# Dawn unittests
###############################################################################

dawn_json_generator("mock_webgpu_gen") {
  target = "mock_api"
  outputs = [
    "src/dawn/mock_webgpu.h",
    "src/dawn/mock_webgpu.cpp",
  ]
}

# Source code for mocks used for unit testing are separated from the rest of
# sources so that they aren't included in non-test builds.
source_set("native_mocks_sources") {
  testonly = true

  deps = [
    ":gmock_and_gtest",
    "${dawn_root}/src/dawn/native:sources",
    "${dawn_root}/src/dawn/native:static",
    "${dawn_root}/src/dawn/utils",
  ]

  # Add internal dawn native config for internal unittests.
  configs += [ "${dawn_root}/src/dawn/native:internal" ]

  sources = [
    "../dawn/tests/unittests/native/mocks/BindGroupLayoutMock.h",
    "../dawn/tests/unittests/native/mocks/BindGroupMock.h",
    "../dawn/tests/unittests/native/mocks/CommandBufferMock.h",
    "../dawn/tests/unittests/native/mocks/ComputePipelineMock.h",
    "../dawn/tests/unittests/native/mocks/DeviceMock.h",
    "../dawn/tests/unittests/native/mocks/ExternalTextureMock.h",
    "../dawn/tests/unittests/native/mocks/PipelineLayoutMock.h",
    "../dawn/tests/unittests/native/mocks/QuerySetMock.h",
    "../dawn/tests/unittests/native/mocks/RenderPipelineMock.h",
    "../dawn/tests/unittests/native/mocks/SamplerMock.h",
    "../dawn/tests/unittests/native/mocks/ShaderModuleMock.cpp",
    "../dawn/tests/unittests/native/mocks/ShaderModuleMock.h",
    "../dawn/tests/unittests/native/mocks/SwapChainMock.h",
    "../dawn/tests/unittests/native/mocks/TextureMock.h",
  ]
}

dawn_test("dawn_unittests") {
  deps = [
    ":gmock_and_gtest",
    ":mock_webgpu_gen",
    ":native_mocks_sources",
    "${dawn_root}/src/dawn:cpp",
    "${dawn_root}/src/dawn:proc",
    "${dawn_root}/src/dawn/common",
    "${dawn_root}/src/dawn/native:sources",
    "${dawn_root}/src/dawn/native:static",
    "${dawn_root}/src/dawn/utils",
    "${dawn_root}/src/dawn/wire",
  ]

  # Add internal dawn native config for internal unittests.
  configs = [ "${dawn_root}/src/dawn/native:internal" ]

  sources = get_target_outputs(":mock_webgpu_gen")
  sources += [
    "${dawn_root}/src/dawn/wire/client/ClientMemoryTransferService_mock.cpp",
    "${dawn_root}/src/dawn/wire/client/ClientMemoryTransferService_mock.h",
    "${dawn_root}/src/dawn/wire/server/ServerMemoryTransferService_mock.cpp",
    "${dawn_root}/src/dawn/wire/server/ServerMemoryTransferService_mock.h",
    "../dawn/tests/DawnNativeTest.cpp",
    "../dawn/tests/DawnNativeTest.h",
    "../dawn/tests/MockCallback.h",
    "../dawn/tests/ToggleParser.cpp",
    "../dawn/tests/ToggleParser.h",
    "../dawn/tests/unittests/AsyncTaskTests.cpp",
    "../dawn/tests/unittests/BitSetIteratorTests.cpp",
    "../dawn/tests/unittests/BuddyAllocatorTests.cpp",
    "../dawn/tests/unittests/BuddyMemoryAllocatorTests.cpp",
    "../dawn/tests/unittests/ChainUtilsTests.cpp",
    "../dawn/tests/unittests/CommandAllocatorTests.cpp",
    "../dawn/tests/unittests/ConcurrentCacheTests.cpp",
    "../dawn/tests/unittests/EnumClassBitmasksTests.cpp",
    "../dawn/tests/unittests/EnumMaskIteratorTests.cpp",
    "../dawn/tests/unittests/ErrorTests.cpp",
    "../dawn/tests/unittests/FeatureTests.cpp",
    "../dawn/tests/unittests/GPUInfoTests.cpp",
    "../dawn/tests/unittests/GetProcAddressTests.cpp",
    "../dawn/tests/unittests/ITypArrayTests.cpp",
    "../dawn/tests/unittests/ITypBitsetTests.cpp",
    "../dawn/tests/unittests/ITypSpanTests.cpp",
    "../dawn/tests/unittests/ITypVectorTests.cpp",
    "../dawn/tests/unittests/LimitsTests.cpp",
    "../dawn/tests/unittests/LinkedListTests.cpp",
    "../dawn/tests/unittests/MathTests.cpp",
    "../dawn/tests/unittests/ObjectBaseTests.cpp",
    "../dawn/tests/unittests/PerStageTests.cpp",
    "../dawn/tests/unittests/PerThreadProcTests.cpp",
    "../dawn/tests/unittests/PlacementAllocatedTests.cpp",
    "../dawn/tests/unittests/RefBaseTests.cpp",
    "../dawn/tests/unittests/RefCountedTests.cpp",
    "../dawn/tests/unittests/ResultTests.cpp",
    "../dawn/tests/unittests/RingBufferAllocatorTests.cpp",
    "../dawn/tests/unittests/SerialMapTests.cpp",
    "../dawn/tests/unittests/SerialQueueTests.cpp",
    "../dawn/tests/unittests/SlabAllocatorTests.cpp",
    "../dawn/tests/unittests/StackContainerTests.cpp",
    "../dawn/tests/unittests/SubresourceStorageTests.cpp",
    "../dawn/tests/unittests/SystemUtilsTests.cpp",
    "../dawn/tests/unittests/ToBackendTests.cpp",
    "../dawn/tests/unittests/TypedIntegerTests.cpp",
    "../dawn/tests/unittests/native/CommandBufferEncodingTests.cpp",
    "../dawn/tests/unittests/native/DestroyObjectTests.cpp",
    "../dawn/tests/unittests/native/DeviceCreationTests.cpp",
    "../dawn/tests/unittests/validation/BindGroupValidationTests.cpp",
    "../dawn/tests/unittests/validation/BufferValidationTests.cpp",
    "../dawn/tests/unittests/validation/CommandBufferValidationTests.cpp",
    "../dawn/tests/unittests/validation/ComputeIndirectValidationTests.cpp",
    "../dawn/tests/unittests/validation/ComputeValidationTests.cpp",
    "../dawn/tests/unittests/validation/CopyCommandsValidationTests.cpp",
    "../dawn/tests/unittests/validation/CopyTextureForBrowserTests.cpp",
    "../dawn/tests/unittests/validation/DebugMarkerValidationTests.cpp",
    "../dawn/tests/unittests/validation/DeviceValidationTests.cpp",
    "../dawn/tests/unittests/validation/DrawIndirectValidationTests.cpp",
    "../dawn/tests/unittests/validation/DrawVertexAndIndexBufferOOBValidationTests.cpp",
    "../dawn/tests/unittests/validation/DynamicStateCommandValidationTests.cpp",
    "../dawn/tests/unittests/validation/ErrorScopeValidationTests.cpp",
    "../dawn/tests/unittests/validation/ExternalTextureTests.cpp",
    "../dawn/tests/unittests/validation/GetBindGroupLayoutValidationTests.cpp",
    "../dawn/tests/unittests/validation/IndexBufferValidationTests.cpp",
    "../dawn/tests/unittests/validation/InternalUsageValidationTests.cpp",
    "../dawn/tests/unittests/validation/LabelTests.cpp",
    "../dawn/tests/unittests/validation/MinimumBufferSizeValidationTests.cpp",
    "../dawn/tests/unittests/validation/MultipleDeviceTests.cpp",
    "../dawn/tests/unittests/validation/OverridableConstantsValidationTests.cpp",
    "../dawn/tests/unittests/validation/PipelineAndPassCompatibilityTests.cpp",
    "../dawn/tests/unittests/validation/QueryValidationTests.cpp",
    "../dawn/tests/unittests/validation/QueueOnSubmittedWorkDoneValidationTests.cpp",
    "../dawn/tests/unittests/validation/QueueSubmitValidationTests.cpp",
    "../dawn/tests/unittests/validation/QueueWriteBufferValidationTests.cpp",
    "../dawn/tests/unittests/validation/QueueWriteTextureValidationTests.cpp",
    "../dawn/tests/unittests/validation/RenderBundleValidationTests.cpp",
    "../dawn/tests/unittests/validation/RenderPassDescriptorValidationTests.cpp",
    "../dawn/tests/unittests/validation/RenderPipelineValidationTests.cpp",
    "../dawn/tests/unittests/validation/ResourceUsageTrackingTests.cpp",
    "../dawn/tests/unittests/validation/SamplerValidationTests.cpp",
    "../dawn/tests/unittests/validation/ShaderModuleValidationTests.cpp",
    "../dawn/tests/unittests/validation/StorageTextureValidationTests.cpp",
    "../dawn/tests/unittests/validation/TextureSubresourceTests.cpp",
    "../dawn/tests/unittests/validation/TextureValidationTests.cpp",
    "../dawn/tests/unittests/validation/TextureViewValidationTests.cpp",
    "../dawn/tests/unittests/validation/ToggleValidationTests.cpp",
    "../dawn/tests/unittests/validation/UnsafeAPIValidationTests.cpp",
    "../dawn/tests/unittests/validation/ValidationTest.cpp",
    "../dawn/tests/unittests/validation/ValidationTest.h",
    "../dawn/tests/unittests/validation/VertexBufferValidationTests.cpp",
    "../dawn/tests/unittests/validation/VertexStateValidationTests.cpp",
    "../dawn/tests/unittests/validation/VideoViewsValidationTests.cpp",
    "../dawn/tests/unittests/validation/WriteBufferTests.cpp",
    "../dawn/tests/unittests/wire/WireAdapterTests.cpp",
    "../dawn/tests/unittests/wire/WireArgumentTests.cpp",
    "../dawn/tests/unittests/wire/WireBasicTests.cpp",
    "../dawn/tests/unittests/wire/WireBufferMappingTests.cpp",
    "../dawn/tests/unittests/wire/WireCreatePipelineAsyncTests.cpp",
    "../dawn/tests/unittests/wire/WireDestroyObjectTests.cpp",
    "../dawn/tests/unittests/wire/WireDisconnectTests.cpp",
    "../dawn/tests/unittests/wire/WireErrorCallbackTests.cpp",
    "../dawn/tests/unittests/wire/WireExtensionTests.cpp",
    "../dawn/tests/unittests/wire/WireInjectDeviceTests.cpp",
    "../dawn/tests/unittests/wire/WireInjectInstanceTests.cpp",
    "../dawn/tests/unittests/wire/WireInjectSwapChainTests.cpp",
    "../dawn/tests/unittests/wire/WireInjectTextureTests.cpp",
    "../dawn/tests/unittests/wire/WireInstanceTests.cpp",
    "../dawn/tests/unittests/wire/WireMemoryTransferServiceTests.cpp",
    "../dawn/tests/unittests/wire/WireOptionalTests.cpp",
    "../dawn/tests/unittests/wire/WireQueueTests.cpp",
    "../dawn/tests/unittests/wire/WireShaderModuleTests.cpp",
    "../dawn/tests/unittests/wire/WireTest.cpp",
    "../dawn/tests/unittests/wire/WireTest.h",
    "../dawn/tests/unittests/wire/WireWGPUDevicePropertiesTests.cpp",
  ]

  if (is_win) {
    sources += [ "../dawn/tests/unittests/WindowsUtilsTests.cpp" ]
  }

  if (dawn_enable_d3d12) {
    sources += [ "../dawn/tests/unittests/d3d12/CopySplitTests.cpp" ]
  }

  # When building inside Chromium, use their gtest main function because it is
  # needed to run in swarming correctly.
  if (build_with_chromium) {
    deps += [ ":unittests_main" ]
  } else {
    sources += [ "../dawn/tests/UnittestsMain.cpp" ]
  }
}

###############################################################################
# Dawn end2end tests targets
###############################################################################

source_set("end2end_tests_sources") {
  configs += [ "${dawn_root}/src/dawn/common:internal_config" ]
  testonly = true

  deps = [
    ":gmock_and_gtest",
    "${dawn_root}/src/dawn:cpp",
    "${dawn_root}/src/dawn:proc",
    "${dawn_root}/src/dawn/common",

    # Statically linked because the end2end white_box tests use Dawn internals.
    "${dawn_root}/src/dawn/native:static",
    "${dawn_root}/src/dawn/utils",
    "${dawn_root}/src/dawn/wire",
  ]

  sources = [
    "../dawn/tests/DawnTest.h",
    "../dawn/tests/MockCallback.h",
    "../dawn/tests/ParamGenerator.h",
    "../dawn/tests/ToggleParser.cpp",
    "../dawn/tests/ToggleParser.h",
    "../dawn/tests/end2end/AdapterDiscoveryTests.cpp",
    "../dawn/tests/end2end/BasicTests.cpp",
    "../dawn/tests/end2end/BindGroupTests.cpp",
    "../dawn/tests/end2end/BufferTests.cpp",
    "../dawn/tests/end2end/BufferZeroInitTests.cpp",
    "../dawn/tests/end2end/ClipSpaceTests.cpp",
    "../dawn/tests/end2end/ColorStateTests.cpp",
    "../dawn/tests/end2end/CommandEncoderTests.cpp",
    "../dawn/tests/end2end/CompressedTextureFormatTests.cpp",
    "../dawn/tests/end2end/ComputeCopyStorageBufferTests.cpp",
    "../dawn/tests/end2end/ComputeDispatchTests.cpp",
    "../dawn/tests/end2end/ComputeLayoutMemoryBufferTests.cpp",
    "../dawn/tests/end2end/ComputeSharedMemoryTests.cpp",
    "../dawn/tests/end2end/ComputeStorageBufferBarrierTests.cpp",
    "../dawn/tests/end2end/CopyTests.cpp",
    "../dawn/tests/end2end/CopyTextureForBrowserTests.cpp",
    "../dawn/tests/end2end/CreatePipelineAsyncTests.cpp",
    "../dawn/tests/end2end/CullingTests.cpp",
    "../dawn/tests/end2end/DebugMarkerTests.cpp",
    "../dawn/tests/end2end/DeprecatedAPITests.cpp",
    "../dawn/tests/end2end/DepthBiasTests.cpp",
    "../dawn/tests/end2end/DepthStencilCopyTests.cpp",
    "../dawn/tests/end2end/DepthStencilLoadOpTests.cpp",
    "../dawn/tests/end2end/DepthStencilSamplingTests.cpp",
    "../dawn/tests/end2end/DepthStencilStateTests.cpp",
    "../dawn/tests/end2end/DestroyTests.cpp",
    "../dawn/tests/end2end/DeviceInitializationTests.cpp",
    "../dawn/tests/end2end/DeviceLostTests.cpp",
    "../dawn/tests/end2end/DrawIndexedIndirectTests.cpp",
    "../dawn/tests/end2end/DrawIndexedTests.cpp",
    "../dawn/tests/end2end/DrawIndirectTests.cpp",
    "../dawn/tests/end2end/DrawTests.cpp",
    "../dawn/tests/end2end/DynamicBufferOffsetTests.cpp",
    "../dawn/tests/end2end/EntryPointTests.cpp",
    "../dawn/tests/end2end/ExternalTextureTests.cpp",
    "../dawn/tests/end2end/FirstIndexOffsetTests.cpp",
    "../dawn/tests/end2end/GpuMemorySynchronizationTests.cpp",
    "../dawn/tests/end2end/IndexFormatTests.cpp",
    "../dawn/tests/end2end/MaxLimitTests.cpp",
    "../dawn/tests/end2end/MemoryAllocationStressTests.cpp",
    "../dawn/tests/end2end/MultisampledRenderingTests.cpp",
    "../dawn/tests/end2end/MultisampledSamplingTests.cpp",
    "../dawn/tests/end2end/NonzeroBufferCreationTests.cpp",
    "../dawn/tests/end2end/NonzeroTextureCreationTests.cpp",
    "../dawn/tests/end2end/ObjectCachingTests.cpp",
    "../dawn/tests/end2end/OpArrayLengthTests.cpp",
    "../dawn/tests/end2end/PipelineLayoutTests.cpp",
    "../dawn/tests/end2end/PrimitiveStateTests.cpp",
    "../dawn/tests/end2end/PrimitiveTopologyTests.cpp",
    "../dawn/tests/end2end/QueryTests.cpp",
    "../dawn/tests/end2end/QueueTests.cpp",
    "../dawn/tests/end2end/QueueTimelineTests.cpp",
    "../dawn/tests/end2end/ReadOnlyDepthStencilAttachmentTests.cpp",
    "../dawn/tests/end2end/RenderAttachmentTests.cpp",
    "../dawn/tests/end2end/RenderBundleTests.cpp",
    "../dawn/tests/end2end/RenderPassLoadOpTests.cpp",
    "../dawn/tests/end2end/RenderPassTests.cpp",
    "../dawn/tests/end2end/SamplerFilterAnisotropicTests.cpp",
    "../dawn/tests/end2end/SamplerTests.cpp",
    "../dawn/tests/end2end/ScissorTests.cpp",
    "../dawn/tests/end2end/ShaderFloat16Tests.cpp",
    "../dawn/tests/end2end/ShaderTests.cpp",
    "../dawn/tests/end2end/StorageTextureTests.cpp",
    "../dawn/tests/end2end/SubresourceRenderAttachmentTests.cpp",
    "../dawn/tests/end2end/Texture3DTests.cpp",
    "../dawn/tests/end2end/TextureFormatTests.cpp",
    "../dawn/tests/end2end/TextureSubresourceTests.cpp",
    "../dawn/tests/end2end/TextureViewTests.cpp",
    "../dawn/tests/end2end/TextureZeroInitTests.cpp",
    "../dawn/tests/end2end/VertexFormatTests.cpp",
    "../dawn/tests/end2end/VertexOnlyRenderPipelineTests.cpp",
    "../dawn/tests/end2end/VertexStateTests.cpp",
    "../dawn/tests/end2end/ViewportOrientationTests.cpp",
    "../dawn/tests/end2end/ViewportTests.cpp",
  ]

  # Validation tests that need OS windows live in end2end tests.
  sources += [
    "../dawn/tests/unittests/validation/ValidationTest.cpp",
    "../dawn/tests/unittests/validation/ValidationTest.h",
  ]

  libs = []

  if (dawn_enable_d3d12) {
    sources += [
      "../dawn/tests/end2end/D3D12CachingTests.cpp",
      "../dawn/tests/end2end/D3D12ResourceWrappingTests.cpp",
      "../dawn/tests/end2end/VideoViewsTests_win.cpp",
    ]
    libs += [
      "d3d11.lib",
      "dxgi.lib",
    ]
  }

  if (dawn_enable_metal) {
    sources += [ "../dawn/tests/end2end/IOSurfaceWrappingTests.cpp" ]
    frameworks = [ "IOSurface.framework" ]
  }

  if (dawn_enable_opengl) {
    assert(dawn_supports_glfw_for_windowing)
  }

  if (dawn_supports_glfw_for_windowing) {
    sources += [
      "../dawn/tests/end2end/SwapChainTests.cpp",
      "../dawn/tests/end2end/SwapChainValidationTests.cpp",
      "../dawn/tests/end2end/WindowSurfaceTests.cpp",
    ]
    deps += [ "${dawn_root}/src/dawn/utils:glfw" ]
  }

  if (dawn_enable_d3d12 || (dawn_enable_vulkan && is_chromeos)) {
    sources += [
      "../dawn/tests/end2end/VideoViewsTests.cpp",
      "../dawn/tests/end2end/VideoViewsTests.h",
    ]
  }

  if (dawn_enable_vulkan && is_chromeos) {
    sources += [ "../dawn/tests/end2end/VideoViewsTests_gbm.cpp" ]
  }
}

source_set("white_box_tests_sources") {
  configs += [ "${dawn_root}/src/dawn/native:internal" ]
  testonly = true

  deps = [
    ":gmock_and_gtest",
    "${dawn_root}/src/dawn:cpp",
    "${dawn_root}/src/dawn:proc",
    "${dawn_root}/src/dawn/common",
    "${dawn_root}/src/dawn/native:sources",

    # Statically linked because the end2end white_box tests use Dawn internals.
    "${dawn_root}/src/dawn/native:static",
    "${dawn_root}/src/dawn/utils",
    "${dawn_root}/src/dawn/wire",
  ]

  sources = [
    "../dawn/tests/DawnTest.h",
    "../dawn/tests/ParamGenerator.h",
    "../dawn/tests/ToggleParser.h",
  ]

  if (dawn_enable_vulkan) {
    deps += [ "${dawn_root}/third_party/khronos:vulkan_headers" ]

    if (is_chromeos) {
      sources += [
        "../dawn/tests/white_box/VulkanImageWrappingTests.cpp",
        "../dawn/tests/white_box/VulkanImageWrappingTests.h",
        "../dawn/tests/white_box/VulkanImageWrappingTests_DmaBuf.cpp",
      ]
    } else if (is_linux) {
      sources += [
        "../dawn/tests/white_box/VulkanImageWrappingTests.cpp",
        "../dawn/tests/white_box/VulkanImageWrappingTests.h",
        "../dawn/tests/white_box/VulkanImageWrappingTests_OpaqueFD.cpp",
      ]
    }

    if (dawn_enable_error_injection) {
      sources += [ "../dawn/tests/white_box/VulkanErrorInjectorTests.cpp" ]
    }
  }

  sources += [
    "../dawn/tests/white_box/BufferAllocatedSizeTests.cpp",
    "../dawn/tests/white_box/InternalResourceUsageTests.cpp",
    "../dawn/tests/white_box/InternalStorageBufferBindingTests.cpp",
    "../dawn/tests/white_box/QueryInternalShaderTests.cpp",
  ]

  if (dawn_enable_d3d12) {
    sources += [
      "../dawn/tests/white_box/D3D12DescriptorHeapTests.cpp",
      "../dawn/tests/white_box/D3D12ResidencyTests.cpp",
      "../dawn/tests/white_box/D3D12ResourceHeapTests.cpp",
    ]
  }

  if (dawn_enable_metal) {
    sources += [ "../dawn/tests/white_box/MetalAutoreleasePoolTests.mm" ]
  }

  if (dawn_enable_opengl) {
    deps += [ "${dawn_root}/src/dawn/utils:glfw" ]
  }

  if (dawn_enable_opengles) {
    sources += [ "../dawn/tests/white_box/EGLImageWrappingTests.cpp" ]
    deps += [ "//third_party/angle:libEGL" ]
  }

  libs = []
}

dawn_test("dawn_end2end_tests") {
  deps = [
    ":end2end_tests_sources",
    ":gmock_and_gtest",
    ":white_box_tests_sources",
    "${dawn_root}/src/dawn:cpp",
    "${dawn_root}/src/dawn:proc",
    "${dawn_root}/src/dawn/common",
    "${dawn_root}/src/dawn/native:static",
    "${dawn_root}/src/dawn/utils",
    "${dawn_root}/src/dawn/wire",
  ]

  sources = [
    "../dawn/tests/DawnTest.cpp",
    "../dawn/tests/DawnTest.h",
  ]

  libs = []

  # When building inside Chromium, use their gtest main function because it is
  # needed to run in swarming correctly.
  if (build_with_chromium) {
    deps += [ ":end2end_tests_main" ]
  } else {
    sources += [ "../dawn/tests/End2EndTestsMain.cpp" ]
  }

  if (dawn_enable_opengl) {
    deps += [ "${dawn_root}/src/dawn/utils:glfw" ]
  }

  if (is_chromeos) {
    libs += [ "gbm" ]
  }
}

###############################################################################
# Dawn perf tests
###############################################################################

dawn_test("dawn_perf_tests") {
  deps = [
    ":gmock_and_gtest",
    "${dawn_root}/src/dawn:cpp",
    "${dawn_root}/src/dawn:proc",
    "${dawn_root}/src/dawn/common",
    "${dawn_root}/src/dawn/native",
    "${dawn_root}/src/dawn/platform",
    "${dawn_root}/src/dawn/utils",
    "${dawn_root}/src/dawn/wire",
  ]

  sources = [
    "../dawn/tests/DawnTest.cpp",
    "../dawn/tests/DawnTest.h",
    "../dawn/tests/ParamGenerator.h",
    "../dawn/tests/ToggleParser.cpp",
    "../dawn/tests/ToggleParser.h",
    "../dawn/tests/perf_tests/BufferUploadPerf.cpp",
    "../dawn/tests/perf_tests/DawnPerfTest.cpp",
    "../dawn/tests/perf_tests/DawnPerfTest.h",
    "../dawn/tests/perf_tests/DawnPerfTestPlatform.cpp",
    "../dawn/tests/perf_tests/DawnPerfTestPlatform.h",
    "../dawn/tests/perf_tests/DrawCallPerf.cpp",
    "../dawn/tests/perf_tests/ShaderRobustnessPerf.cpp",
    "../dawn/tests/perf_tests/SubresourceTrackingPerf.cpp",
  ]

  libs = []

  # When building inside Chromium, use their gtest main function and the
  # other perf test scaffolding in order to run in swarming correctly.
  if (build_with_chromium) {
    deps += [ ":perf_tests_main" ]
    data_deps = [ "//testing:run_perf_test" ]
  } else {
    sources += [ "../dawn/tests/PerfTestsMain.cpp" ]
  }

  if (dawn_enable_metal) {
    frameworks = [ "IOSurface.framework" ]
  }

  if (dawn_enable_opengl) {
    deps += [ "${dawn_root}/src/dawn/utils:glfw" ]
  }
}
