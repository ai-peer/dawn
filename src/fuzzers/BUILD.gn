# Copyright 2018 The Dawn Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("../../scripts/dawn_overrides_with_defaults.gni")

static_library("dawn_spirv_cross_fuzzer_common") {
  sources = [
    "DawnSPIRVCrossFuzzer.cpp",
    "DawnSPIRVCrossFuzzer.h",
  ]
  public_deps = [
    "${dawn_root}/third_party/gn/spirv_cross:spirv_cross",
    "${dawn_spirv_tools_dir}:spvtools_val",
  ]
}

static_library("dawn_wire_server_fuzzer_common") {
  sources = [
    "DawnWireServerFuzzer.cpp",
    "DawnWireServerFuzzer.h",
  ]
  public_deps = [
    "${dawn_root}/src/common",
    "${dawn_root}/src/dawn:dawn_proc",
    "${dawn_root}/src/dawn:dawncpp",
    "${dawn_root}/src/dawn_native:dawn_native_static",
    "${dawn_root}/src/dawn_wire:dawn_wire_static",
    "${dawn_root}/src/utils:dawn_utils",
  ]
}

# TODO(rharrison): Remove asan_options once signal trap is no longer
#                  needed.
# Uses Dawn specific options and varies input data
fuzzer_test("dawn_spirv_cross_glsl_fast_fuzzer") {
  sources = [ "DawnSPIRVCrossGLSLFastFuzzer.cpp" ]
  deps = [ ":dawn_spirv_cross_fuzzer_common" ]
  asan_options = [ "allow_user_segv_handler=1" ]
}

fuzzer_test("dawn_wire_server_and_frontend_fuzzer") {
  sources = [ "DawnWireServerAndFrontendFuzzer.cpp" ]

  deps = [ ":dawn_wire_server_fuzzer_common" ]

  additional_configs = [ "${dawn_root}/src/common:dawn_internal" ]
}

if (is_win) {
  fuzzer_test("dawn_wire_server_and_d3d12_backend_fuzzer") {
    sources = [ "DawnWireServerAndD3D12BackendFuzzer.cpp" ]

    deps = [ ":dawn_wire_server_fuzzer_common" ]

    additional_configs = [ "${dawn_root}/src/common:dawn_internal" ]
  }
}

fuzzer_test("dawn_wire_server_and_vulkan_backend_fuzzer") {
  sources = [ "DawnWireServerAndVulkanBackendFuzzer.cpp" ]

  deps = [ ":dawn_wire_server_fuzzer_common" ]

  additional_configs = [ "${dawn_root}/src/common:dawn_internal" ]
}

# A group target to build all the fuzzers
group("dawn_fuzzers") {
  testonly = true
  deps = [
    ":dawn_spirv_cross_glsl_fast_fuzzer",
    ":dawn_wire_server_and_frontend_fuzzer",
    ":dawn_wire_server_and_vulkan_backend_fuzzer",
  ]

  if (is_win) {
    deps += [ ":dawn_wire_server_and_d3d12_backend_fuzzer" ]
  }
}
